/*
 * Copyright (c) 2023 Alexander Zagniotov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

testing {
    suites {
        test {
            useTestNG()
            dependencies {
                implementation "com.google.truth:truth:1.1.5"
                // From v7.6.0 (inc.) Java 8 support is dropped
                implementation "org.testng:testng:7.5.1"
            }
        }

        integrationTest(JvmTestSuite) {
            sources {
                resources {
                    srcDirs += [
                            "src/main/resources"
                    ]
                }
            }
            useJUnit()
            dependencies {
                implementation project()
                implementation "junit:junit:4.13.2"
                implementation "org.apache.lucene:lucene-test-framework:${solrVersion}"
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }

        functionalTest(JvmTestSuite) {

            sources {
                resources {
                    srcDirs += [
                            "src/main/resources"
                    ]
                }
            }
            useJUnit()
            dependencies {
                implementation project()
                implementation "org.apache.lucene:lucene-queryparser:${solrVersion}"
                implementation "org.apache.lucene:lucene-test-framework:${solrVersion}"
                implementation "junit:junit:4.13.2"
            }

            targets {
                all {
                    testTask.configure {
                        systemProperty "test.solr.allowed.securerandom", "NativePRNG"
                        systemProperty "solr.log.dir", "/var/solr/logs"
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

processIntegrationTestResources.dependsOn(processResources)

tasks.withType(Test).configureEach {


    reports.html.required = false
    reports.junitXml.required = false
    ignoreFailures = false
    testLogging {
        showStandardStreams true
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}
