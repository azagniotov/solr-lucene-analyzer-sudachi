/*
 * Copyright 2023 https://github.com/apache/lucene
 * Concept and modifications 2023 Alexander Zagniotov
 *
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "de.undercouch.download"

plugins.withType(JavaPlugin) {
    ext {
        targetDir = file("/tmp/sudachi")
        dictionarySuffix = "full"
        dictionaryName = "sudachi-dictionary-20230927-${dictionarySuffix}"
        downloadDestination = "${rootDir}/.sudachi/downloaded/${dictionaryName}.zip"
        dictChecksum = "85d90c6c2235690f07fe859d737b2cb9"
    }

    task deleteDictionaryData() {
        doFirst {
            delete fileTree(dir: targetDir, include: "**/*.dict")
            delete fileTree(dir: targetDir, include: "**/*.csv")
        }
    }

    task downloadFile(type: Download) {
        description "Downloads a Sudachi dictionary from AWS or uses rpeviously downloaded"

        dependsOn deleteDictionaryData
        dependsOn sourceSets.main.runtimeClasspath

        def dictionarySource = "http://sudachi.s3-website-ap-northeast-1.amazonaws.com/sudachidict/${dictionaryName}.zip"
        def dictionaryFile = file("${downloadDestination}")
        def unpackedDir = file("${targetDir}/system-dict")

        src dictionarySource
        dest dictionaryFile

        quiet false
        overwrite false
        onlyIfModified true

        doLast {
            // Unpack the downloaded archive.
            delete unpackedDir
            ant.unzip(src: dictionaryFile, dest: unpackedDir) {
                ant.cutdirsmapper(dirs: "1")
            }

            ant.copy file: "${rootDir}/user-dictionary/user_lexicon.csv", todir: targetDir
            ant.move file: "${unpackedDir}/system_${dictionarySuffix}.dic", tofile: "${unpackedDir}/system.dict"
        }
    }

    task buildUserDictionary(type: JavaExec, dependsOn: downloadFile) {
        classpath = sourceSets.main.runtimeClasspath

        mainClass = "com.worksap.nlp.sudachi.dictionary.UserDictionaryBuilder"

        // arguments to pass to the application
        args "-o",
                "${targetDir}/user_lexicon.dict",
                "-s",
                "${targetDir}/system-dict/system.dict",
                "${targetDir}/user_lexicon.csv"
    }

    task configureDictionariesLocally(type: Verify, dependsOn: buildUserDictionary) {
        src "${downloadDestination}"
        algorithm "MD5"
        checksum dictChecksum
    }
}
